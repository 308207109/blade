import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'org.dm.gradle:gradle-bundle-plugin:0.6.1'
	}
}

repositories {
	mavenCentral()	
	/* for pulling latest liferay artifacts */
	maven {
		
		url "http://cdn.repository.liferay.com/nexus/content/groups/public"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.dm.bundle'

sourceCompatibility = 1.7
version = '1.0'

dependencies {
    compile 'javax.portlet:portlet-api:2.0'
	compile 'javax.servlet:javax.servlet-api:3.1.0'
	compile 'org.osgi:org.osgi.compendium:5.0.0'
	compile 'org.osgi:org.osgi.core:5.0.0'
	compile 'org.osgi:org.osgi.annotation:6.0.0'
	compile 'com.liferay.portal:portal-service:7.0.0-m2-SNAPSHOT'
	
}

bundle {
	instructions << [
		'-sources' : 'true',
		'Bundle-Version' : '1.0.0.${tstamp}',
		'Import-Package' :  """\
			javax.portlet;version='[2.0,3)',\
			javax.servlet;version='[2.5,4)',\
			org.osgi.framework;version='[1.7,2)',\
			*""",
		'-dsannotations':'*'
	]
}

defaultTasks 'clean', 'deployBundle'

def liferayHome = _projectProperty('liferay.home') ?: _systemProperty('liferay.home')

if(liferayHome == null){
	
	throw new StopExecutionException("liferay.home undefined, define a system property or set liferay.home in gradle.properties")
	
}

def liferayModulesDir = new File(liferayHome+"/osgi/modules")

task deployBundle(dependsOn: 'jar', type: Copy) {
	from "$buildDir/libs"
	into liferayModulesDir
	include '*.jar'	
}

task wrapper(type:Wrapper){
	gradleVersion = '2.0'
}

def _projectProperty(String propertyName){
  return this.properties[propertyName]
}

def _systemProperty(String propertyName){
	return System.properties[propertyName]
}
